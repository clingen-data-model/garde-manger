(ns garde-manger.gene-dosage
  (:require [clj-http.client :as http]
            [cheshire.core :as json]
            [clojure.set :refer [rename-keys]]))


;; JIRA maintains custom fields for the PMID links and descriptions that are used
;; as evidence to justify the interpretation
;; These are listed as pairs, the first is a PMID reference, the second is
;; a textual description
(def loss-evidence-fields
  [["customfield_10183" "customfield_10184"]
   ["customfield_10185" "customfield_10186"]
   ["customfield_10187" "customfield_10188"]])

(def gain-evidence-fields
  [["customfield_10189" "customfield_10190"]
   ["customfield_10191" "customfield_10192"]
   ["customfield_10193" "customfield_10194"]])

(def loss-fields {"customfield_10200" :phenotype
                  "customfield_10165" :haploinsufficiency_score
                  "customfield_10198" :description})

(def gain-fields {"customfield_10166" :triplosensitivity_score
                  "customfield_10201" :gain_phenotype})

(def gene-fields {"customfield_10157" :ncbi_gene})

(def frontmatter-fields {"title" :title
                         "updated" :date
                         "status" :status})

;; Fields where we want to extract a more deeply nested value to represent
;; in the JSON, or otherwise apply some appropriate transformation
;; a map of functions to apply to the value of certain keys after retrieval
(def valued-keys [[:haploinsufficiency_score #(% "value")]
                  [:status #(% "name")]
                  [:phenotype #(if (empty? %)
                                 nil
                                 (str "http://purl.obolibrary.org/obo/OMIM_" %))]])

(defn extract-evidence-line
  [fields key-set]
  (for [e key-set
        :let [pub (fields (first e))
              desc (fields (second e))]
        :when pub]
    {:publication pub, :description desc}))

(defn transform-gene-haploinsufficiency
  "Transform an assertion generated by JIRA into a standard format"
  [item]
  (let [id (item "key")
        fields (item "fields")
        evidence (extract-evidence-line fields loss-evidence-fields)
        key-set (merge frontmatter-fields gene-fields loss-fields)
        mapped-fields (-> fields (rename-keys key-set) (select-keys (vals key-set)))
        ;; apply transformations specified in valued-keys
        mapped-valued-fields (reduce #(update %1 (%2 0) (%2 1)) mapped-fields valued-keys)]
    (merge {:id id}
           {:evidence_line  evidence}
           mapped-valued-fields)))

(defn transform-jira-issues
  "Transform issues from JIRA into data model-ish format"
  []
  (let [query-str "project = ISCA AND type = \"ISCA Gene Curation\" AND \"ISCA Haploinsufficiency score\" in (0,1,2,3, \"30: Gene associated with autosomal recessive phenotype\") ORDER BY updated DESC"
        url "https://ncbijira.ncbi.nlm.nih.gov/rest/api/2/search"
        result (http/get url {:query-params 
                              {:jql query-str
                               :startAt 0
                               :maxResults 5}
                              :content-type "application/json"
                              :basic-auth ["thnelson@geisinger.edu", "***REMOVED***"]})
        issues (-> result :body json/parse-string (get "issues"))]
    (map transform-gene-haploinsufficiency issues)))


