(ns garde-manger.gene-dosage
  (:require [clj-http.client :as http]
            [cheshire.core :as json]
            [clojure.set :refer [rename-keys]]
            [garde-manger.kafka :as kafka])
  (:import java.util.Properties
           [org.apache.kafka.clients.producer KafkaProducer Producer ProducerRecord]))

;; Topic to use for gene_dosage 
(def kafka-topic "gene_dosage")
(def dosage-root "https://search.clinicalgenome.org/kb/gene-dosage/")
(def start-date "2011-01-01")

;; JIRA maintains custom fields for the PMID links and descriptions that are used
;; as evidence to justify the interpretation
;; These are listed as pairs, the first is a PMID reference, the second is
;; a textual description
(def loss-evidence-fields
  [["customfield_10183" "customfield_10184" "-EL-H1"]
   ["customfield_10185" "customfield_10186" "-EL-H2"]
   ["customfield_10187" "customfield_10188" "-EL-H3"]])

(def gain-evidence-fields
  [["customfield_10189" "customfield_10190" "-EL-T1"]
   ["customfield_10191" "customfield_10192" "-EL-T2"]
   ["customfield_10193" "customfield_10194" "-EL-T3"]])


;; Mappings for custom fields; specific to loss or gain, gene or region
(def loss-fields {"customfield_10200" :phenotype
                  "customfield_10165" :interpretation
                  "customfield_10198" :description})

(def gain-fields {"customfield_10166" :interpretation
                  "customfield_10201" :phenotype
                  "customfield_10199" :description})

(def gene-fields {"customfield_10157" :gene})

(def frontmatter-fields {"title" :title
                         "updated" :modified
                         "status" :status})

;; Translation of raw scores into IRI-based types
;; note difference between interpretations for loss vs gain
(def loss-interp 
  {"3" "http://datamodel.clinicalgenome.org/terms/CG_000092"
   "2" "http://datamodel.clinicalgenome.org/terms/CG_000093"
   "1" "http://datamodel.clinicalgenome.org/terms/CG_000095"
   "0" "http://datamodel.clinicalgenome.org/terms/CG_000096"
   "30: Gene associated with autosomal recessive phenotype" "http://datamodel.clinicalgenome.org/terms/CG_000094"
   "40: Dosage sensitivity unlikely" "http://datamodel.clinicalgenome.org/terms/CG_000120"})

(def gain-interp
  {"3" "http://datamodel.clinicalgenome.org/terms/CG_000097"
   "2" "http://datamodel.clinicalgenome.org/terms/CG_000098"
   "1" "http://datamodel.clinicalgenome.org/terms/CG_000099"
   "0" "http://datamodel.clinicalgenome.org/terms/CG_000100"
   "40: Dosage sensitivity unlikely" "http://datamodel.clinicalgenome.org/terms/CG_000121"})

(def status-codes
  {"Closed" "http://datamodel.clinicalgenome.org/terms/CG_000114"
   "Under Group Review" "http://datamodel.clinicalgenome.org/terms/CG_000117"
   "Open" "http://datamodel.clinicalgenome.org/terms/CG_000119"
   "Reopened" "http://datamodel.clinicalgenome.org/terms/CG_000118"
   "Under Primary Review" "http://datamodel.clinicalgenome.org/terms/CG_000115"
   "Under Secondary Review" "http://datamodel.clinicalgenome.org/terms/CG_000116"})

(def loss-evidence "http://datamodel.clinicalgenome.org/terms/CG_000111")
(def gain-evidence "http://datamodel.clinicalgenome.org/terms/CG_000112")

;; Fields where we want to extract a more deeply nested value to represent
;; in the JSON, or otherwise apply some appropriate transformation
;; a map of functions to apply to the value of certain keys after retrieval
(def valued-keys [[:interpretation #(% "value")]
                  [:status #(% "name")]
                  [:phenotype #(if (empty? %)
                                 nil
                                 (str "http://purl.obolibrary.org/obo/OMIM_" %))]])

(defn send-message
  "Send an updated dosage curation into Kafka"
  [p k v]
  (.send p (ProducerRecord. kafka-topic k v)))

(defn extract-evidence-line
  "Given keyset, identifying the evidence and description fields for the assertion,
  extract the evidence line and the description"
  [fields key-set type id]
  (for [e key-set
        :let [pub (fields (first e))
              desc (fields (second e))
              suffix (e 2)]
        :when pub]
    {:id (str id suffix)
     :information (str "https://www.ncbi.nlm.nih.gov/pubmed/" pub)
     :description desc
     :type type}))

(defn transform-gene-haploinsufficiency
  "Transform an assertion generated by JIRA into a standard format"
  [item]
  (let [id (str dosage-root (item "key") "-H")
        fields (item "fields")
        evidence (extract-evidence-line fields loss-evidence-fields loss-evidence id)
        key-set (merge frontmatter-fields gene-fields loss-fields)
        mapped-fields (-> fields (rename-keys key-set) (select-keys (vals key-set)))
        ;; apply transformations specified in valued-keys
        mapped-valued-fields (reduce #(update %1 (%2 0) (%2 1)) mapped-fields valued-keys)
        interpreted-fields (->  mapped-valued-fields 
                                (update :interpretation loss-interp)
                                (update :status status-codes))]
    (merge {:id id}
           {:evidence  evidence}
           {:type "http://datamodel.clinicalgenome.org/terms/CG_000083"}
           interpreted-fields)))

(defn transform-gene-triplosensitivity
  "Transform the triplosensitivity side of interpretations into a standard format"
  [item]
  (let [id (str dosage-root (item "key") "-T")
        fields (item "fields")
        evidence (extract-evidence-line fields gain-evidence-fields gain-evidence id)
        key-set (merge frontmatter-fields gene-fields gain-fields)
        mapped-fields (-> fields (rename-keys key-set) (select-keys (vals key-set)))
        ;; apply transformations specified in valued-keys
        mapped-valued-fields (reduce #(update %1 (%2 0) (%2 1)) mapped-fields valued-keys)
        interpreted-fields (->  mapped-valued-fields 
                                (update :interpretation gain-interp)
                                (update :status status-codes))]
    (merge {:id id}
           {:evidence_line  evidence}
           {:type "http://datamodel.clinicalgenome.org/terms/CG_000083"}
           interpreted-fields)))


                                        ;0,1,2,3, \"30: Gene associated with autosomal recessive phenotype\", \"40: Dosage sensitivity unlikely\"

;; "startAt" 0,
;; "maxResults" 1,
;; "total" 45596,


(defn jira-issues
  "Return a lazy seq of blocks of raw issues, retrieved from JIRA"
  [start-time start max-results]
  (let [query-str "project = ISCA AND type = \"ISCA Gene Curation\" AND status != Open AND resolution = Complete ORDER BY updated DESC"
        url "https://ncbijira.ncbi.nlm.nih.gov/rest/api/2/search"
        result (http/get url {:query-params 
                              {:jql query-str
                               :startAt start
                               :maxResults max-results}
                              :content-type "application/json"
                              :basic-auth ["thnelson@geisinger.edu", "***REMOVED***"]})
        result-body (-> result :body json/parse-string)
        remaining (- (result-body "total") (+ start max-results))]
    (if (> 0 remaining)
      (lazy-seq
       (cons (result-body "issues")
             (jira-issues start-time (+ start max-results) max-results)))
      )))

(defn transform-issues
  "Transform issues from JIRA into data model-ish format"
  [issues]
  (into (map transform-gene-haploinsufficiency issues)
        (map transform-gene-triplosensitivity issues)))

(defn push-messages
  "Push incoming messages to Kafka-based data exchange"
  [messages]
  (with-open [p (kafka/producer)]
    (doseq [m messages]
      (.send p (ProducerRecord. "gene_dosage" (:id m) (json/generate-string m))))))

(defn write-messages
  "Write incoming messages to file"
  [messages out-file]
  (with-open [w (clojure.java.io/writer out-file)]
    (json/generate-stream messages w {:pretty true})))
